package cyan.lang

 final object Int
    @javacode<<<
        public int n = 0;
        public CyInt(int n) { this.n = n; }
        static public CyInt zero = new CyInt(0);
        static public CyInt one = new CyInt(1);
        static public CyInt two = new CyInt(2);

    >>>

    func init {
        @javacode{* n = 0; *}
    }
    


    override
    func eq:  (Dyn other) -> Boolean { 
        @javacode<<< 
            try {
                return new CyBoolean(n == (((CyInt ) _other).n)) ; 
            }
            catch ( ClassCastException e ) {
            }
        >>> 
        return false;
    }

    override
    func neq: (Dyn other) -> Boolean { 
        @javacode<<< 
            try {
                return new CyBoolean(n != (((CyInt ) _other).n)) ; 
            }
            catch ( ClassCastException e ) {
            }
        >>> 
        return false;
    }
    


    func +  (Int other) -> Int { @javacode<<< return new CyInt(n + _other.n); >>> }
    func -  (Int other) -> Int { @javacode<<< return new CyInt(n - _other.n); >>> }
    func *  (Int other) -> Int { @javacode<<< return new CyInt(n * _other.n); >>> }
    func /  (Int other) -> Int { @javacode<<< return new CyInt(n / _other.n); >>> }
    func %  (Int other) -> Int { @javacode<<< return new CyInt(n % _other.n); >>> }
    func <  (Int other) -> Boolean { @javacode<<< return new CyBoolean(n <  _other.n); >>> }
    func <= (Int other) -> Boolean { @javacode<<< return new CyBoolean(n <= _other.n); >>> }
    func >  (Int other) -> Boolean { @javacode<<< return new CyBoolean(n >  _other.n); >>> }
    func >= (Int other) -> Boolean { @javacode<<< return new CyBoolean(n >= _other.n); >>> }

   
    func pred -> Int {
        @javacode{* return new CyInt( n - 1 ); *}
    }

    func succ -> Int {
        @javacode{* return new CyInt( n + 1 ); *}
    }

    func odd -> Boolean {
        @javacode<<< return new CyBoolean(n%2 != 0); >>> 
    }
    
    func even -> Boolean {
        @javacode<<< return new CyBoolean(n%2 == 0); >>> 
    }
    
    func prime -> Boolean {
        @javacode<<< return new CyBoolean(isPrimeNumber(n)); >>> 
    
    }
    
    func maxValue -> Int {
       @javacode{* return new CyInt( Integer.MAX_VALUE ); *}
    }
    

    func minValue -> Int {
       @javacode{* return new CyInt( Integer.MIN_VALUE ); *}
    }
    
    
    override
    func == (Dyn other)  -> Boolean { 
        @javacode<<< 
            long other;
            if ( _other instanceof CyByte ) 
                other = ((CyByte ) _other).n;
            else if ( _other instanceof CyInt )
                other = ((CyInt ) _other).n;
            else if ( _other instanceof CyShort )
                other = ((CyShort ) _other).n;
            else if ( _other instanceof CyLong ) {
                other = ((CyLong ) _other).n;
            }
            else if ( _other instanceof CyFloat ) {
                float otherFloat = ((CyFloat ) _other).n;
                if ( (float ) n == otherFloat )
                    return CyBoolean.cyTrue;
                else
                    return CyBoolean.cyFalse;
            }
            else if ( _other instanceof CyDouble ) {
                double otherDouble = ((CyDouble ) _other).n;
                if ( (double ) n == otherDouble )
                    return CyBoolean.cyTrue;
                else
                    return CyBoolean.cyFalse;
            }
            else
                return CyBoolean.cyFalse;
                
            if ( other == (long ) n )
                return CyBoolean.cyTrue;
            else
                return CyBoolean.cyFalse;
        >>> 
    }
    override
    func != (Dyn other)  -> Boolean { 
        if self == other { return false } else { return true }
    }
     

    
    /*  return -1 if self < other, 0 if self == other, and 1 if self > other
    */
    func <=> (Int other) -> Int {  
        if self < other { return -1 } 
        else if self > other { return 1 }
        else { 
           return 0
        }
    }
    func .. (Int theEnd) -> Interval<Int> {  
        return Interval<Int> new: self, theEnd
    } 
    func ..< (Int theEnd) -> Interval<Int> {  
        return Interval<Int> new: self, (theEnd - 1);
    } 

       // all unary operators are prefixed
    func -  -> Int { @javacode<<< return new CyInt(-n); >>> }
    func +  -> Int { @javacode<<< return this; >>> }
        // and bit to bit
    func & (Int other) -> Int { @javacode<<< return new CyInt(n & _other.n); >>> }
        // or bit to bit
    func | (Int other) -> Int { @javacode<<< return new CyInt(n | _other.n); >>> }
        // exclusive or bit to bit
    func ~| (Int other) -> Int { @javacode<<< return new CyInt(n ^ _other.n); >>> }
        // binary not
    func ~ -> Int { @javacode<<< return new CyInt(~n); >>> }
      // left shift. The same as << in Java
    func <.< (Int other) -> Int { @javacode<** return new CyInt(n << _other.n); **> }
      // right shift. The same as >> in Java
    func >.> (Int other) -> Int { @javacode<** return new CyInt(n >> _other.n); **> }
          // right shift. The same as >>> in Java
    func >.>> (Int other) -> Int { @javacode<** return new CyInt(n >>> _other.n); **> }

    func |> Function<Int, Int> f -> Int {
        return f eval: self
    }

    func asByte    -> Byte { @javacode<<< return new CyByte( (byte) n ); >>> }
    func asShort   -> Short { @javacode<<< return new CyShort( (short) n ); >>> }
    func asLong    -> Long { @javacode<<< return new CyLong( (long) n); >>> }
    func asFloat   -> Float { @javacode<<< return new CyFloat( (float) n ); >>> }
    func asDouble  -> Double { @javacode<<< return new CyDouble( (double ) n ); >>> }
    func asChar    -> Char { @javacode<<< return new CyChar( (char ) n); >>> }
    func asBoolean -> Boolean { @javacode<<< return new CyBoolean( n == 0 ? false : true ); >>> }

      // strange, I know, but this is used to make generic prototype Interval<T> simpler
    func asInt     -> Int = self;
    
    override
    func asString  -> String { @javacode<<< return new CyString(n + ""); >>> }

    func to: (Int max) do: (Function<Int, Nil> b) {
        var Int i = self;
        while i <= max {
            b eval: i;
            ++i
        }
    }



       // injection method to be used with context object.
       // the initial value is private to injectTo
    func to: (Int max) into: (InjectObject<Int> injectTo) {
        var Int i = self;
        while i <= max {
            injectTo eval: i;
            ++i
        };
    }
    
    func times: Function<Nil> b {
        var Int i = 0;
        while i < self {
            b eval;
            ++i;
        }
    }
    func repeat: Function<Int, Nil> b {
        var i = 0;
        while i < self {
            b eval: i;
            ++i;
        }
    }

    func to: (Int max)
        inject: (Int initialValue)
        into: (Function<Int, Int, Int> b) -> Int {
        
        var Int i = self;
        var Int total = initialValue;
        while i <= max {
            total = b eval: total, i;
            ++i
        };
        return total
    }

    func in: (Iterable<Int> container) -> Boolean {
        for item in container {
            if self == item {
                return true;
            }
        };
        return false;
    }

    func between: Interval<Int> inter -> Boolean {
        return self >= inter first && self <= inter last
    }

    override
    func hashCode -> Int { 
        @javacode<<<
            return new CyInt(n);
        >>>
    }
    


    func defaultValue -> Int {
        return 0;
    }       
    
    @javacode{*
        // taken from https://java67.blogspot.com/2014/01/how-to-check-if-given-number-is-prime.html
        private static boolean isPrimeNumber(int number) {
            if (number == 2 || number == 3) {
                return true;
            }
            if (number % 2 == 0 || number == 1) {
                return false;
            }
            int sqrt = (int) Math.sqrt(number) + 1;
            for (int i = 3; i < sqrt; i += 2) {
                if (number % i == 0) {
                    return false;
                }
            }
            return true;
        }
    
    *}    
    
end

